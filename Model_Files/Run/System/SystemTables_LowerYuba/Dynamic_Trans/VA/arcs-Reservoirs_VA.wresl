/*
File Name: arcs-reservoirs_VA.wresl
CalSim 3.0 System File

Purpose:


*/

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! New Bullards Bar Reservoir, North Fork  Yuba River  !!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
define S_NBLDB_1_VA     {std kind 'STORAGE-ZONE' units 'TAF'}
define S_NBLDB_2_VA     {std kind 'STORAGE-ZONE' units 'TAF'}
define S_NBLDB_3_VA     {std kind 'STORAGE-ZONE' units 'TAF'}
define S_NBLDBlevel4_VA {
	case yuba_VA {
		condition SimulateVA_Yub >= 0.5 .and. wyt_sac >= 2 .and. wyt_sac <= 4 		!Use VA Target line for AN, BN, D years
			select target from res_level where res_num=93,level=6,month=month }
	case otherwise {condition always select target from res_level where res_num=93,level=4,month=month} !Use Accord Target line for W and C years
}
Define S_NBLDBLevel4_DV_VA {alias S_NBLDBlevel4_VA kind 'STORAGE' units 'TAF'}
define S_NBLDBlevel3_VA {value min(S_NBLDBlevel3_,S_NBLDBlevel4_VA)}
define S_NBLDB_4_VA     {std kind 'STORAGE-ZONE' units 'TAF'}
define S_NBLDB_5_VA     {std kind 'STORAGE-ZONE' units 'TAF'}
define S_NBLDB_6_VA     {std kind 'STORAGE-ZONE' units 'TAF'}
define S_NBLDB_VA       {std kind 'STORAGE' units 'TAF'}
define E_NBLDB_VA       {lower unbounded kind 'EVAPORATION' units 'CFS'}
define A_NBLDB_VA       {std kind 'SURFACE-AREA' units 'ACRES'}
define A_NBLDBlast_VA   {select area from YubaBear_res_info given storage=1000*S_NBLDB_VA(-1) use linear where res_num=11}
define A_NBLDBforward_VA {select area from YubaBear_res_info given storage=1050*S_NBLDB_VA(-1) use linear where res_num=11}
define A_NBLDBback_VA   {select area from YubaBear_res_info given storage=950*S_NBLDB_VA(-1) use linear where res_num=11}
define coefev_NBLDB_VA  {                                          !Z.Q. Richard Chen 5/8/2017 
	case NEARZERO { condition S_NBLDB_VA(-1) <= 0.001
		select coefev from res_info given storage=1 use minimum where res_num=93}	 
	case Otherwise { condition always 
                value (A_NBLDBforward_VA - A_NBLDBback_VA)/(100*max(0.01,S_NBLDB_VA(-1)))}
        }
define RelCap_NBLDB_VA  {select discharge from YubaBear_res_info given storage=1000*S_NBLDB_VA(-1) use linear where res_num=11}
